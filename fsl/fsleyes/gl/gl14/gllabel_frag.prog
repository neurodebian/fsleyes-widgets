!!ARBfp1.0
#
# Fragment program used for rendering GLLabel instances.
# This is to be used with the glvolume_vert.prog vertex shader.
#
# Inputs:
#   fragment.texcoord[0] - Fragment texture coordinates
#   fragment.texcoord[1] - Fragment voxel coordinates
#
#   program.local[0]
#   program.local[1]
#   program.local[2]
#   program.local[3]     - Matrix which transforms image texture
#                          data into the original data range
#   program.local[4]     - Image shape
#   program.local[5]     - 1.0 / (number of labels in the lookup table)
#   program.local[6]     - If the x component is non-zero, show region
#                          outlines. The yzw components specify the
#                          border/outline widths along each axis.
#
# Outputs:
#   result.color         - Fragment colour.
#
# Author: Paul McCarthy <pauldmccarthy@gmail.com>
#

TEMP  voxCoord;
TEMP  lutCoord;
TEMP  invNumLabels;
TEMP  voxValue;

PARAM imageShape    = {{ param_imageShape   }};
MOV   invNumLabels,   {{ param_invNumLabels }};
PARAM outline       = {{ param_outline      }};

# This matrix scales the voxel value to
# lie in a range which is appropriate to
# the current display range 
PARAM voxValXform[4] = {{ param4_voxValXform }};

# retrieve the voxel coordinates,
# bail if they are are out of bounds  
MOV voxCoord, {{ varying_voxCoord }};

#pragma include test_in_bounds.prog

# look up image voxel value
# from 3D image texture
TEX voxValue, {{ varying_texCoord }}, {{ texture_imageTexture }}, 3D;

# Scale the texture value
# to its original voxel value
MOV lutCoord, voxValue;
MAD lutCoord, lutCoord, voxValXform[0].x, voxValXform[3].x;

# Scale the voxel value to
# a lut texture coordinate
ADD lutCoord, lutCoord, { 0.5, 0, 0, 0 };
MUL lutCoord, lutCoord, invNumLabels;

# look up the appropriate colour
# in the 1D colour map texture
TEX result.color, lutCoord.x, {{ texture_lutTexture }}, 1D;


# Test whether this fragment lies 
# on an edge between label regions.

TEMP coord;
TEMP val;
TEMP tol;
TEMP offsets;

MOV coord, {{ varying_texCoord }};
MOV val,   voxValue;
MOV tol,   invNumLabels.x;
MUL tol,   tol, 0.001;

MOV offsets, outline.yzww;

#pragma include edge3D.prog


# Figure out if we want to fill the
# fragment, or kill the fragment.
TEMP fill;

# Set fill to 1 if there is an edge 
# along any dimension, 0 otherwise
MAX fill, isEdge.x, isEdge.y;
MAX fill, fill,     isEdge.z;

# 
# If outlines are enabled, and
# this fragment is not on an edge,
# kill the fragment.
# 
# Simiarly, if outlines are disabled
# and this fragment is on an edge,
# kill the fragment.
#
# This boils down to testing whether
# isEdge == outline.x
# 
SUB fill, fill, outline.x;
ABS fill, fill;

# Fill is now 0 if isEdge == outline.x,
# 1 otherwise

MAD fill, fill, -1, 0.5;
KIL fill;

  
END
