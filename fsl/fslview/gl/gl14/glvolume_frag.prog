!!ARBfp1.0
#
# Fragment program used for rendering GLVolume instances.
#
# This fragment program does the following:
# 
#  1. Retrieves the texture coordinates corresponding to the fragment
# 
#  2. Transforms those coordinates into voxel coordinates
# 
#  3. Uses those voxel coordinates to look up the corresponding voxel
#     value in the 3D image texture.
# 
#  4. Uses that voxel value to look up the corresponding colour in the
#     1D colour map texture.
# 
#  5. Sets the fragment colour.
#
# Author: Paul McCarthy <pauldmccarthy@gmail.com>
#

TEMP  dispTexCoord;
TEMP  voxTexCoord;
TEMP  normVoxTexCoord;
TEMP  voxValue;
TEMP  voxColour;
PARAM imageShape    = program.local[0];
PARAM imageShapeInv = program.local[1];

# This matrix scales the voxel value to
# lie in a range which is appropriate to
# the current display range 
PARAM voxValXform[4] = { state.matrix.texture[1] };

# This matrix transforms coordinates
# from the display coordinate system
# to image voxel coordinates
PARAM dispToVoxMat[4] = { state.matrix.texture[0] };

# retrieve the 3D texture coordinates
# (which are in terms of the display
# coordinate system)
MOV dispTexCoord, fragment.texcoord[0];

# Transform said coordinates
# into voxel coordinates
DP4 voxTexCoord.x, dispToVoxMat[0], dispTexCoord;
DP4 voxTexCoord.y, dispToVoxMat[1], dispTexCoord;
DP4 voxTexCoord.z, dispToVoxMat[2], dispTexCoord;

# Offset voxel coordinates by 0.5 
# so they are centred within a voxel
ADD voxTexCoord, voxTexCoord, { 0.5, 0.5, 0.5, 0.0 };

# Normalise voxel coordinates to 
# lie in the range (0, 1), so they 
# can be used for texture lookup
MUL normVoxTexCoord, voxTexCoord, imageShapeInv;

# look up image voxel value
# from 3D image texture
TEX voxValue, normVoxTexCoord, texture[0], 3D;

# Scale voxel value according
# to the current display range
MUL voxValue, voxValue, voxValXform[0].x;
ADD voxValue, voxValue, voxValXform[0].w;

# look up the appropriate colour
# in the 1D colour map texture
TEX voxColour, voxValue.x, texture[1], 1D;

# If any of the voxel coordinates are
# less than 0, clear the voxel colour
CMP voxColour.w, voxTexCoord.x, 0.0, voxColour.w;
CMP voxColour.w, voxTexCoord.y, 0.0, voxColour.w;
CMP voxColour.w, voxTexCoord.z, 0.0, voxColour.w;

# If any voxel coordinates are greater than
# the image shape, clear the voxel colour
SUB voxTexCoord, voxTexCoord, imageShape;
CMP voxColour.w, voxTexCoord.x, voxColour.w, 0.0;
CMP voxColour.w, voxTexCoord.y, voxColour.w, 0.0;
CMP voxColour.w, voxTexCoord.z, voxColour.w, 0.0;

# Colour the pixel!
MOV result.color, voxColour;

END
